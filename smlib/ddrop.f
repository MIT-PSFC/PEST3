C******************** START FILE DDROP.FOR ; GROUP FILTR6 ******************
C------------------------------------------------------------
C
C  DDROP   SUBROUTINE TO DROP OUT DATA POINTS THAT DIVERGE
C          TOO FAR FROM THEIR IMMEDIATE NEIGHBOURS, I.E.,
C          TO REMOVE 'SPIKES' FROM THE DATA SIGNAL.
C
C    X AND Y ARE ARRAYS CONTAINING THE ABSCISSAE AND ORDINATES
C    OF THE DATA SIGNAL, RESPECTIVELY, AND N IS THE
C    NUMBER OF DATA POINTS.
C      DDV, IN PHYSICAL UNITS (UNITS OF THE DATA ORDINATES)
C    IS THE DATA DROP CRITERION.  EACH ORDINATE (EXCEPT THE
C    ENDPOINT ORDINATES) IS COMPARED WITH THE VALUE LIN-
C    EARLY INTERPOLATED FROM THE POINT'S TWO IMMEDIATE
C    NEIGHBORS.  IF THE DIFFERENCE IS MORE THAN DDV,  THE
C    ORDINATE IS CONSIDERED A 'SPIKE', ELIGIBLE FOR REMOVAL.
C    IF THE SPIKE IS ISOLATED FROM OTHER SPIKES, SO THAT
C    THE ALGORITHM CAN REASONABLY DISTINGUISH BETWEEN SPIKE
C    AND SIGNAL, THE SPIKE IS REMOVED BY SUBSTITUTING THE
C   LINEARLY INTERPOLATED VALUE.
C
C  IF CONSECUTIVE POINTS VIOLATE THE DDV CRITERION, IT MAY BE
C  THAT THE ENTIRE SIGNAL CONSISTS OF SPIKES, AND NO ACTION
C   WILL BE TAKEN UNLESS BY CORRECTING ONE OF THE ORDINATES,
C  THE ENTIRE SEQUENCE OF VIOLATIONS CAN BE REMOVED (NOTE THAT
C  FOR A SINGLE SPIKE WHICH IS LARGE ENOUGH, A VIOLATION MAY
C  BE DETECTED AT THE SPIKE POINT ITSELF AND ITS IMMEDIATE
C  NEIGHBORS, RESULTING IN THREE CONSECUTIVE VIOLATIONS)
C
C--------------------------------------------------------
C  WRITTEN D. MC CUNE @PPPL   JAN 10 1979
C   REVISED:
C
C
C
C------------------------------------------------------------
C
      SUBROUTINE DDROP(X,Y,N,DDV,NDROP)
C
C  NDROP=NUMBER OF POINTS 'DROPPED'  (OUTPUT)
C
      DIMENSION X(N),Y(N),DYTES(5)
C
C  START OF EXECUTABLE CODE --
C
C  STATEMENT FUNCTION:
C
      YLININ(XX,X1,Y1,X2,Y2)=Y1+(XX-X1)*(Y2-Y1)/(X2-X1)
C
C  SET UP
C
      NDROP=0
      NM1=N-1
      IF(N.LT.3) RETURN
      NA1=1
      NA2=5
      DYTES(1)=0.
      DO 10 I=2,5
      DYTES(I)=0.
      IF(I.GT.NM1) GO TO 10
      IM1=I-1
      IP1=I+1
      DYTES(I)=Y(I)-YLININ(X(I),X(IM1),Y(IM1),X(IP1),Y(IP1))
 10   CONTINUE
C
C  MAIN LOOP
C
C  THE FIVE ELEMENT ARRAY DYTES IS A MOVING 'WINDOW' ON THE
C  DATA, CONTAINING THE LOCAL 'SPIKE HEIGHT' FOR DATA ORDINATES
C  NA1 THRU NA2.  WHEN A SPIKE HEIGHT VIOLATION IS DETECTED
C  AND DETERMINED TO BE ISOLATED FROM OTHER VIOLATIONS THRU THIS
C  WINDOW, IT IS PROCESSED.  ANY PLACE WHERE FOUR OR MORE
C  CONSECUTIVE VIOLATIONS OCCUR WILL NOT BE PROCESSED
C
 20   CONTINUE
C  TEST FOR COMPLETION
      IF(NA1.GE.NM1) GO TO 200
C  TEST FOR CONTINUATION OF SEQUENCE OF VIOLATIONS TOO LENGTHY
C  TO CORRECT
      IF(ABS(DYTES(1)).GE.DDV) GO TO 150
C  LOOK FOR NEW VIOLATION
      DO 30 I=2,4
      IF(ABS(DYTES(I)).GE.DDV) GO TO 40
 30   CONTINUE
C  NO VIOLATION FOUND
      GO TO 150
C  FIND END OF SEQUENCE OF VIOLATIONS (IN WINDOW)
 40   CONTINUE
      NV1=NA1+I-1
      DO 50 J=I+1,5
      IF(ABS(DYTES(J)).LT.DDV) GO TO 60
 50   CONTINUE
C  END OF SEQUENCE OF VIOLATIONS NOT FOUND
      GO TO 150
C  END OF SEQUENCE OF VIOLATIONS FOUND; SEQUENCE IS AT MOST
C  THREE DATA POINTS LONG
 60   CONTINUE
      NV2=NA1+J-2
      NVIOL=NV2-NV1+1
      GO TO (70,80,100) NVIOL
C
C  ISOLATED (SINGLE POINT) VIOLATION
C
 70   CONTINUE
      Y(NV1)=Y(NV1)-DYTES(I)
      NDROP=NDROP+1
      DYTES(I)=0.
      GO TO 150
C
C  VIOLATEION INVOLVING TWO CONSECUTIVE DATA POINTS
C
 80   CONTINUE
      NV=NV1
      NVC=NV2
      IV=I
      IF(ABS(DYTES(I+1)).LT.ABS(DYTES(I))) GO TO 90
      NV=NV2
      NVC=NV1
      IV=I+1
C
 90   CONTINUE
C  APPLY CORRECTION FOR LARGER VIOLATION ONLY IF THIS
C  ALSO REMOVES THE VIOLATION FOR THE OTHER POINT AS WELL
C
      YSAV=Y(NV)
      Y(NV)=Y(NV)-DYTES(IV)
      NCM1=NVC-1
      NCP1=NVC+1
      DYNEW=Y(NVC)-YLININ(X(NVC),X(NCM1),Y(NCM1),X(NCP1),Y(NCP1))
      IF(ABS(DYNEW).GE.DDV) Y(NV)=YSAV
      IF(Y(NV).EQ.YSAV) GO TO 150
      NDROP=NDROP+1
      DYTES(I)=0.
      DYTES(I+1)=0.
      GO TO 150
C
C  VIOLATION INVOLVING THREE CONSECUTIVE DATA POINTS
C  TRY CORRECTING MIDDLE VIOLATING POINT ONLY AND
C  APPLY ONLY IF THIS REMOVES THE VIOLATION FOR ALL THREE
C  POINTS
C
 100  CONTINUE
      NV=NV1+1
      IV=I+1
      YSAV=Y(NV)
      Y(NV)=Y(NV)-DYTES(IV)
      NCM1=NV1-1
      NCP1=NV2+1
      DYNEW=Y(NV1)-YLININ(X(NV1),X(NCM1),Y(NCM1),X(NV),Y(NV))
      IF(ABS(DYNEW).GE.DDV) Y(NV)=YSAV
      DYNEW=Y(NV2)-YLININ(X(NV2),X(NV),Y(NV),X(NCP1),Y(NCP1))
      IF(ABS(DYNEW).GE.DDV) Y(NV)=YSAV
      IF(YSAV.EQ.Y(NV)) GO TO 150
      NDROP=NDROP+1
      DO 110 J=I,I+2
      DYTES(J)=0.
 110  CONTINUE
      GO TO 150
C
C  END OF MAIN LOOP ... ADVANCE WINDOW
C
 150  CONTINUE
      NA1=NA1+1
      DO 160 I=1,4
      DYTES(I)=DYTES(I+1)
 160  CONTINUE
      DYTES(5)=0.
      NA2=NA1+4
      NAM1=NA2-1
      NP1=NA2+1
      IF(NA2.LE.NM1)
     >  DYTES(5)=Y(NA2)-YLININ(X(NA2),X(NAM1),Y(NAM1),X(NP1),Y(NP1))
      GO TO 20
C
C  LOOP EXIT ***
C
 200  CONTINUE
      RETURN
C
C
      	END
C******************** END FILE DDROP.FOR ; GROUP FILTR6 ******************
